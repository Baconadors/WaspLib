(*
FairyRing
=========
*)

{$DEFINE WL_FAIRYRING_INCLUDED}
{$IFNDEF WL_OSR}
  {$I WaspLib/osr.simba}
{$ENDIF}

type
  ERSFairyRingDial = (LEFT, MIDDLE, RIGHT);

  TRSFairyRing = record (TRSInterface)
    Dials: array [ERSFairyRingDial] of String;
    TravelLog: TRSInterface;
  end;

(*
FairyRing.Setup
~~~~~~~~~~~~~~~
.. pascal:: procedure FairyRing.Setup();

Initializes TRSFairyRing variables.

.. note:: This is automatically called on the **FairyRing** variable.
*)
procedure TRSFairyRing.Setup(); override;
const
  STRINGS: TStringArray = ['adcb', 'ilkj', 'psrq'];
var
  i: Int32;
begin
  inherited;

  Self.Name := 'FairyRing';
  for i := 0 to High(STRINGS) do
    Self.Dials[ERSFairyRingDial(i)] := STRINGS[i];

  Self.TravelLog.Setup();
  Self.TravelLog.Name := 'FairyRingTravelLog';
end;

procedure TRSFairyRing.SetupAlignment(mode: ERSClientMode); override;
begin
  Self.Mode := mode;

  case Self.Mode of
    ERSClientMode.FIXED:
      begin
        Self.Alignment.Left := [@InterfaceArea.X1];
        Self.Alignment.Right := [@InterfaceArea.X2];
        Self.Alignment.Top := [@InterfaceArea.Y1];
        Self.Alignment.Bottom := [@InterfaceArea.Y2];
        Self.Alignment.Center.MaxWidth := 500;
        Self.Alignment.Center.MaxHeight := 320;
      end;

    ERSClientMode.RESIZABLE_CLASSIC, ERSClientMode.RESIZABLE_MODERN:
      begin
        Self.Alignment.Left := [@InterfaceArea.X1];
        Self.Alignment.Right := [@InterfaceArea.X2];
        Self.Alignment.Top := [@InterfaceArea.Y1, 2];
        Self.Alignment.Bottom := [@InterfaceArea.Y2, -2];
        Self.Alignment.Center.MaxWidth := 500;
        Self.Alignment.Center.MaxHeight := 320;
      end;
  end;

  Self.TravelLog.SetupAlignment(mode);
  Self.TravelLog.Alignment.Left := [@GameTab.X1, -3];
  Self.TravelLog.Alignment.Right := [@GameTab.X2, 3];
  Self.TravelLog.Alignment.Top := [@GameTab.Y1];
  Self.TravelLog.Alignment.Bottom := [@GameTab.Y2];
end;

(*
FairyRing.IsOpen
~~~~~~~~~~~~~~~~
.. pascal::
  function TRSFairyRing.IsOpen(): Boolean;

Returns true/false whether the FairyRing interface is open or not.

Example
-------

 WriteLn FairyRing.IsOpen();
*)
function TRSFairyRing.IsOpen(): Boolean;
begin
  Result := InRange(SRL.CountColor(CTS2(3949742, 14, 0.03, 0.70), Self.Bounds()), 18000, 19000);
end;

(*
FairyRing.Close
~~~~~~~~~~~~~~~
.. pascal::
  function TRSFairyRing.Close(pressEscape: Boolean): Boolean;
  function TRSFairyRing.Close(chance: Double = BioHash): Boolean; overload;

Closes the FairyRing interface, depending on `pressEscape` the function will either click the button
or press escape.

Example
-------

 WriteLn FairyRing.Close();
*)
function TRSFairyRing.Close(pressEscape: Boolean): Boolean;
begin
  if not Self.IsOpen() then
    Exit(True);
  Result := MainScreen.ClickCloseButton(pressEscape);
end;

function TRSFairyRing.Close(chance: Double = BioHash): Boolean; overload;
begin
  Result := MainScreen.CloseInterface(chance);
end;


function TRSFairyRing.GetDials(): TBoxArray;
begin
  Result := Grid(3, 1, 159, 159, [11, 0], [Self.X1()+2, Self.Y1()+75]);
  Result[2].X1 -= 2;
  Result[2].X2 -= 2;
end;

function TRSFairyRing.GetDial(dial: ERSFairyRingDial): TBox;
begin
  Result := Self.GetDials()[Ord(dial)];
end;


function TRSFairyRing.GetLetterBox(dial: ERSFairyRingDial): TBox;
begin
  Result := Self.GetDial(dial);
  Result.X1 += 60;
  Result.Y1 += 115;
  Result.X2 -= 60;
  Result.Y2 -= 10;
end;

function TRSFairyRing.GetLetter(dial: ERSFairyRingDial): Char;
begin
  case SRl.CountColor(CTS2(6107727, 4, 0.49, 2.05), Self.GetLetterBox(dial)) of
    52 : Result := 'l';
    63 : Result := 'q';
    65 : Result := 'i';
    66 : Result := 'j';
    80 : Result := 'c';
    81 : Result := 'r';
    85 : Result := 'p';
    98 : Result := 'k';
    100: Result := 's';
    125: Result := 'b';
    172: Result := 'a';
    198: Result := 'd';
    else Result := 'n'; //n for null
  end;
end;

function TRSFairyRing.GetCombination(): String;
begin
  Result := Self.GetLetter(ERSFairyRingDial.LEFT) +
            Self.GetLetter(ERSFairyRingDial.MIDDLE) +
            Self.GetLetter(ERSFairyRingDial.RIGHT);
end;


function TRSFairyRing.GetLeftRotationBox(dial: ERSFairyRingDial): TBox;
begin
  Result := Self.GetDial(dial);
  Result.X2 -= 80;
end;

function TRSFairyRing.GetRightRotationBox(dial: ERSFairyRingDial): TBox;
begin
  Result := Self.GetDial(dial);
  Result.X1 += 80;
end;


function TRSFairyRing.SpinLeft(dial: ERSFairyRingDial): Boolean;
var
  c, tmp: Char;
begin
  Result := Self.IsOpen();
  if Result then
  begin
    c := Self.GetLetter(dial);
    Mouse.Click(Self.GetLeftRotationBox(dial), MOUSE_LEFT);
    Result := WaitUntil((c <> (tmp := Self.GetLetter(dial))) and (tmp <> 'n'), 300, 5000);
  end;
end;

function TRSFairyRing.SpinRight(dial: ERSFairyRingDial): Boolean;
var
  c, tmp: Char;
begin
  Result := Self.IsOpen();
  if Result then
  begin
    c := Self.GetLetter(dial);
    Mouse.Click(Self.GetRightRotationBox(dial), MOUSE_LEFT);
    Result := WaitUntil((c <> (tmp := Self.GetLetter(dial))) and (tmp <> 'n'), 300, 5000);
  end;
end;


function TRSFairyRing.GetTeleportButton(): TBox;
begin
  Result := Self.Bounds();
  Result.X1 += 166;
  Result.Y1 += 247;
  Result.X2 -= 165;
  Result.Y2 -= 20;
end;


function TRSFairyRing.GetLogCodes(out boxes: TBoxArray): TStringArray;
const
  COLORS := [$3F3FFF, $7F7FFF];
var
  tpa: TPointArray;
  atpa: T2DPointArray;
  i: Int32;
  str: String;
begin
  if SRL.FindColors(tpa, COLORS, Self.TravelLog.Bounds()) = 0 then
    Exit;

  atpa := tpa.Cluster(8, 1);
  atpa.SortByY();

  boxes := atpa.ToTBA();
  for i := 0 to High(boxes) do
  begin
    str := OCR.Recognize(boxes[i], TOCRColorFilter.Create(COLORS), RS_FONT_PLAIN_12);
    Result += str.Replace(' ', '').ToLower();

    boxes[i].X2 := Self.TravelLog.X2() - 22;
    if i = High(boxes) then
      boxes[i].Y2 := Self.TravelLog.Y2() - 6
    else
      boxes[i].Y2 := boxes[i+1].Y1 - 7;
  end;
end;

function TRSFairyRing.GetLogCodes(): TStringArray; overload;
const
  COLORS := [$3F3FFF, $7F7FFF];
var
  tpa: TPointArray;
  atpa: T2DPointArray;
  b: TBox;
begin
  if SRL.FindColors(tpa, COLORS, Self.TravelLog.Bounds()) = 0 then
    Exit;

  atpa := tpa.Cluster(8, 1);
  for b in atpa.ToTBA() do
    Result += OCR.Recognize(b, TOCRColorFilter.Create(COLORS), RS_FONT_PLAIN_12);
end;

function TRSFairyRing.LogFind(code: String): Boolean;
  function _DoScrolling(code: String; direction: Boolean): Boolean;
  var
    limit: Int32;
  begin
    if direction then
      limit := 100;

    Mouse.Move(Self.TravelLog.Bounds(), True);
    while Self.TravelLog.CanScroll() and (Self.TravelLog.GetScrollPosition() <> limit) do
    begin
      Mouse.Scroll(Antiban.GetUniqueInt(3, 1, 5), direction);
      if Self.GetLogCodes().Contains(code) then
        Exit(True);
    end;
  end;

var
  i, scroll: Int32;
  direction: Boolean;
begin
  if Length(code) <> 3 then
    Self.Fatal('"' + code.Upper() + '" is not a valid fairy ring code. It must be exactly 3 letters.');

  for i := 1 to Length(code) do
    if not Self.Dials[ERSFairyRingDial(i-1)].Contains(code[i]) then
      Self.Fatal('"' + code.Upper() + '" is not a valid fairy ring code.');

  if Self.GetLogCodes().Contains(code) then
    Exit(True);

  scroll := Self.TravelLog.GetScrollPosition();
  direction := InRange(scroll, 0, 3);
  if not direction then
    direction := not InRange(scroll, 97, 100) and Antiban.BioDice();

  if _DoScrolling(code, direction) then
    Exit(True);

  if InRange(scroll, 0, 3) then
    Exit; //scrolled everything and was not found.

  Result := _DoScrolling(code, not direction);
end;


procedure TRSFairyRing.Draw(bitmap: TMufasaBitmap); override;
var
  dial: ERSFairyRingDial;
  b: TBox;
begin
  if not Self.IsOpen() then
    Exit;

  inherited;

  bitmap.DrawBoxes(Self.GetDials(), $00FFFF);
  for dial := ERSFairyRingDial.LEFT to ERSFairyRingDial.RIGHT do
  begin
    b := Self.GetLeftRotationBox(dial).Expand(-1);
    bitmap.DrawBox(b, $00FF00);
    b := Self.GetRightRotationBox(dial).Expand(-1);
    bitmap.DrawBox(b, $0000FF);
  end;

  bitmap.DrawBox(Self.GetTeleportButton(), $00FFFF);
end;

(*
var FairyRing
~~~~~~~~~~~~~
  Global FairyRing variable.
*)
var
  FairyRing: TRSFairyRing;

procedure TRSClient.ClientModeChanged(); override;
begin
  inherited;

  FairyRing.SetupAlignment(Self.Mode);
end;

procedure TSRL.Setup(); override;
begin
  inherited;

  FairyRing.Setup();
end;

procedure TSRL.Debug(bitmap: TMufasaBitmap); override;
begin
  inherited;

  FairyRing.Draw(bitmap);
end;

function TRSMainScreen.IsVisible(p: TPoint): Boolean; override;
begin
  Result := inherited;

  if not Result or (RSClient.Mode = ERSClientMode.FIXED) then
    Exit;

  if FairyRing.Bounds().Expand(5).Contains(p) and FairyRing.IsOpen() then
    Exit(False);
end;

function TRSMainScreen.IsVisible(tpa: TPointArray; useCenter: Boolean = True): Boolean; override;
begin
  Result := inherited;
  if FairyRing.IsOpen() then
    Exit(False);
end;

