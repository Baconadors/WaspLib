(*
# S2SCompiler
A "Simba to Simba compiler" that compiles multiple scripts into a single one.
This is a very basic script and won't understand things like
"IF" compiler directives.

- `input` is the base file name you want to compile.
- `path` is the path to `input`
- `ERASE_DIRECTIVE_REGEX` is a regex for the lines you want this to ignore when "compiling" your script.
  This is because if you have multiple files including the same file, you will get stuck in a recursive loop.
  Should be easy to modify this to use simple strings instead of regex.

The final result will be placed along thing script with `input` or if
`input` is in the same directory, it will be named:
```pascal
input.Replace('.simba', '-compiled.simba')
```
*)

{$I SRL-T/osr.simba}

var
  input: String = 'launcher.simba';
  path: String = ScriptPath + 'wasp-launcher' + DirectorySeparator + 'launcher' + DirectorySeparator;
  output: String = ScriptPath + 'wasp-launcher' + DirectorySeparator;

const
  ERASE_DIRECTIVE: TStringArray = ['WS_LAUNCHER', 'SRL_OSR', 'SRL_UTILS', 'WL_OSR', 'WL_UTILS'];
  INCLUDE_DIRECTIVES: TStringArray = ['{$I ', '{$INCLUDE', '{$i ', '$include'];
  REPLACES: array of TStringArray = [
    ["Scripts/launcher/'", '{$MACRO CURRENT_DIRECTORY}']
  ];


function GetDirectiveIndex(content: String): Int32;
begin
  for Result := 0 to High(INCLUDE_DIRECTIVES) do
    if content.Contains(INCLUDE_DIRECTIVES[Result]) then
      Exit;
  Result := -1;
end;

function GetIncludeContent(content: String; directiveIndex: Int32): String;
const
  RGXS: String = '\{\$IFNDEF ';
  RGXE: String = '\}([\s\S]*?\{\$ENDIF\})';
var
  filepath, directives: String;
  i: Int32;
begin
  filepath := content.After(INCLUDE_DIRECTIVES[directiveIndex]).Before('}');

  if filepath.StartsWith('WaspLib') or filepath.StartsWith('SRL-T') then
    Result := ReadFileContents(Includepath + filepath)
  else
    Result := ReadFileContents(path + filepath);

  for directives in ERASE_DIRECTIVE do
    Result := ReplaceRegExpr(RGXS + directives + RGXE, Result, '', True);

  filepath := ExtractFilepath(path + filepath).Replace(Apppath, "Apppath + '") + "'";
  filepath := filepath.Replace(DirectorySeparator, "' + DirectorySeparator + '");
  filepath := filepath.Replace(" + ''", '');

  Result := Result.Replace('{$macro CURRENT_DIRECTORY}', filepath);
  Result := Result.Replace('{$MACRO CURRENT_DIRECTORY}', filepath);

  for i := 0 to High(REPLACES) do
    Result := Result.Replace(REPLACES[0,0], REPLACES[0,1]);

  Result := LineEnding + Result + LineEnding;
end;

function ReplaceIncludeDirective(content, sub: String; directiveIndex: Int32): String;
var
  directive: String;
begin
  directive := content.After(INCLUDE_DIRECTIVES[directiveIndex]).Before('}');
  directive := INCLUDE_DIRECTIVES[directiveIndex] + directive + '}';
  Result := content.Replace(directive, sub);
end;

var
  content, sub: String;
  index: Int32;
begin
  if GetProcessParameter('input') <> '' then
    input := GetProcessParameter('input');

  if GetProcessParameter('path') <> '' then
    path := GetProcessParameter('path');

  if GetProcessParameter('output') <> '' then
    output := GetProcessParameter('output');

  if not FileExists(path + input) then
  begin
    WriteLn('Run parameters: ', GetProcessParameters());
    WriteLn('input: ', input);
    WriteLn('path: ', path);
    WriteLn('output: ', output);
    raise 'File ' + path+input + ' does not exist.';
  end;

  content := ReadFileContents(path + input);

  index := GetDirectiveIndex(content);
  while index > -1 do
  begin
    sub := GetIncludeContent(content, index);
    content := ReplaceIncludeDirective(content, sub, index);
    index := GetDirectiveIndex(content);
  end;

  if path = output then
    WriteFileContents(path + input.Replace('.simba', '-compiled.simba'), content, False)
  else
    WriteFileContents(output + input, content, False);

  WriteLn 'File written to ' + output;
end;
